plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "com.google.protobuf" version "0.8.12"
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.note"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    sourceSets {
        test.resources.srcDirs += 'src/test/res'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.activity:activity-compose:1.3.0-alpha06"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha04"
    implementation "androidx.navigation:navigation-compose:1.0.0-alpha10"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0-alpha01"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    annotationProcessor 'androidx.room:room-compiler:2.2.6'
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    // Room
    implementation 'androidx.room:room-runtime:2.2.6'
    implementation 'androidx.room:room-ktx:2.2.6'
    kapt 'androidx.room:room-compiler:2.2.6'

    // Retrofit
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0-alpha06"
    implementation "androidx.datastore:datastore-core:1.0.0-alpha06"
    implementation 'com.google.protobuf:protobuf-javalite:3.15.3'

    // Unit testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.7.0"
    testImplementation "io.mockk:mockk:1.9.2"
}

kapt {
    correctErrorTypes true
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}